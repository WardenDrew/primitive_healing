plugins {
    id 'java'
}

subprojects {
    apply plugin: 'java'
    
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    
    group = project.maven_group
    version = "${project.mod_version}+${project.minecraft_version}"
    
    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release.set(17)
    }

    javadoc.options.addStringOption('Xdoclint:none', '-quiet')
    
    jar {
        manifest {
            attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : project.jar.archiveVersion,
                'Implementation-Title'    : mod_name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Timestamp'               : System.currentTimeMillis(),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On-Minecraft'      : minecraft_version
            ])
        }
        from('LICENSE') {
            rename { "${it}_${mod_name}" }
        }
    }
    
    repositories {
        mavenCentral()
    }
    
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }
}